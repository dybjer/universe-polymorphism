We carve universes Ã  la Agda from a superuniverse Ã  la Palmgren. But
some equalities we would like to be definitional are only
identifications, as expected, and moreover seem to require some amount
of extensionality, which may be unexpected, for the cases of Î£, Î  and W.

\begin{code}

{-# OPTIONS --without-K #-}

open import MLTT
open import SuperUniverse
open import SequenceOfUniversesBase
open import Arithmetic

module SequenceOfUniverses-a-la-Agda where

lift-succ : (n : â„•) â†’ ğ“¥ n â†’ ğ“¥ (succ n)
lift-succ _ = successor.âŒœTâŒ

\end{code}

We want to define a lifting map ğ“¥ m â†’ ğ“¥ (max m n). A direct induction
on m and n doesn't work. So we define it by reduction to an easily
defined lifting map ğ“¥ m â†’ ğ“¥ (m âˆ” k)

\begin{code}

lift-+  : (m k : â„•) â†’ ğ“¥ m â†’ ğ“¥ (m âˆ” k)
lift-+ m zero     a = a
lift-+ m (succ k) a = lift-succ (m âˆ” k) (lift-+ m k a)

lift-â‰¤ : (m n : â„•) â†’ m â‰¤ n â†’ ğ“¥ m â†’ ğ“¥ n
lift-â‰¤ m n le a = b
 where
  k : â„•
  k = n - m [ le ]

  p : m âˆ” k â‰¡ n
  p = plus-comm m k âˆ™ minus-property n m le

  b : ğ“¥ n
  b = transport ğ“¥ p (lift-+ m k a)

lift-L-max : (m n : â„•) â†’ ğ“¥ m â†’ ğ“¥ (max m n)
lift-L-max m n = lift-â‰¤ m (max m n) (left-â‰¤-max m n)

lift-R-max : (m n : â„•) â†’ ğ“¥ n â†’ ğ“¥ (max m n)
lift-R-max m n = lift-â‰¤ n (max m n) (right-â‰¤-max m n)

ğ“¢-succ-eq : (n : â„•) (a : ğ“¥ n) â†’ ğ“¢ (succ n) (lift-succ n a) â‰¡â‚ ğ“¢ n a
ğ“¢-succ-eq n a = refl _

ğ“¢-+-eq : (m k : â„•) (a : ğ“¥ m) â†’ ğ“¢ (m âˆ” k) (lift-+ m k a) â‰¡â‚ ğ“¢ m a
ğ“¢-+-eq m zero     a = refl _
ğ“¢-+-eq m (succ k) a = ğ“¢-+-eq m k a

ğ“¢-â‰¤-eq : (m n : â„•) (le : m â‰¤ n) (a : ğ“¥ m) â†’ ğ“¢ n (lift-â‰¤ m n le a) â‰¡â‚ ğ“¢ m a
ğ“¢-â‰¤-eq m n le a = r
 where
  k : â„•
  k = n - m [ le ]

  p : m âˆ” k â‰¡ n
  p = plus-comm m k âˆ™ minus-property n m le

  q : ğ“¢ (m âˆ” k) (lift-+ m k a) â‰¡â‚ ğ“¢ m a
  q = ğ“¢-+-eq m k a

  r : ğ“¢ n (lift-â‰¤ m n le a) â‰¡â‚ ğ“¢ m a
  r = transportdâ‚ â„• ğ“¥ (Î» l b â†’ ğ“¢ l b â‰¡â‚ ğ“¢ m a) (lift-+ m k a) p q

ğ“¢-L-max-eq : (m n : â„•) (a : ğ“¥ m) â†’ ğ“¢ (max m n) (lift-L-max m n a) â‰¡â‚ ğ“¢ m a
ğ“¢-L-max-eq m n = ğ“¢-â‰¤-eq m (max m n) (left-â‰¤-max m n)

ğ“¢-R-max-eq : (m n : â„•) (b : ğ“¥ n) â†’ ğ“¢ (max m n) (lift-R-max m n b) â‰¡â‚ ğ“¢ n b
ğ“¢-R-max-eq m n = ğ“¢-â‰¤-eq n (max m n) (right-â‰¤-max m n)

\end{code}

We also need the following type coercion:

\begin{code}

ğ“¢-L-max : (m n : â„•) (a : ğ“¥ m) â†’ ğ“¢ (max m n) (lift-L-max m n a) â†’ ğ“¢ m a
ğ“¢-L-max m n a = Id-to-fun (ğ“¢-L-max-eq m n a)

\end{code}

With this we can give the Agda universe structure to the Palmgren superuniverse:

\begin{code}

|â„•â‚€| : (n : â„•) â†’ ğ“¥ n
|â„•â‚| : (n : â„•) â†’ ğ“¥ n
|â„•|  : (n : â„•) â†’ ğ“¥ n
|+|  : (m n : â„•) â†’ ğ“¥ m â†’ ğ“¥ n â†’ ğ“¥ (max m n)
|Î£|  : (m n : â„•) â†’ (a : ğ“¥ m) â†’ (ğ“¢ m a â†’ ğ“¥ n) â†’ ğ“¥ (max m n)
|Î |  : (m n : â„•) â†’ (a : ğ“¥ m) â†’ (ğ“¢ m a â†’ ğ“¥ n) â†’ ğ“¥ (max m n)
|W|  : (m n : â„•) â†’ (a : ğ“¥ m) â†’ (ğ“¢ m a â†’ ğ“¥ n) â†’ ğ“¥ (max m n)
|Id| : (n : â„•) â†’ (a : ğ“¥ n) â†’ ğ“¢ n a â†’ ğ“¢ n a â†’ ğ“¥ n
|U|  : (n : â„•) â†’ ğ“¥ n
|T|  : (n : â„•) â†’ ğ“¥ n â†’ ğ“¥ (succ n)

|â„•â‚€| n       = successor.âŒœâ„•â‚€âŒ
|â„•â‚| n       = successor.âŒœâ„•â‚âŒ
|â„•|  n       = successor.âŒœâ„•âŒ
|+|  m n a b = successor._âŒœ+âŒ_ (lift-L-max m n a) (lift-R-max m n b)
|Î£|  m n a b = successor.âŒœÎ£âŒ   (lift-L-max m n a) (Î» x â†’ lift-R-max m n (b (ğ“¢-L-max m n a x)))
|Î |  m n a b = successor.âŒœÎ âŒ   (lift-L-max m n a) (Î» x â†’ lift-R-max m n (b (ğ“¢-L-max m n a x)))
|W|  m n a b = successor.âŒœWâŒ   (lift-L-max m n a) (Î» x â†’ lift-R-max m n (b (ğ“¢-L-max m n a x)))
|Id| n       = successor.âŒœIdâŒ
|U|  n       = successor.âŒœUâŒ
|T|  n       = successor.âŒœTâŒ

\end{code}

To prove the desired equations, we seem to need some amount of
extensionality, which we assume in the following anonymous module.

\begin{code}

module _ (Î£-ext : is-extensional Î£)
         (Î -ext : is-extensional Î )
         (W-ext : is-extensional W)
       where

  |â„•â‚€|-eq : (n : â„•) â†’ ğ“¢ n (|â„•â‚€| n) â‰¡â‚ â„•â‚€
  |â„•â‚|-eq : (n : â„•) â†’ ğ“¢ n (|â„•â‚| n) â‰¡â‚ â„•â‚
  |â„•|-eq  : (n : â„•) â†’ ğ“¢ n (|â„•| n) â‰¡â‚ â„•
  |+|-eq  : (m n : â„•) â†’ (a : ğ“¥ m) (b : ğ“¥ n) â†’ ğ“¢ (max m n) (|+| m n a b) â‰¡â‚ (ğ“¢ m a + ğ“¢ n b)
  |Î£|-eq  : (m n : â„•) â†’ (a : ğ“¥ m) (b : ğ“¢ m a â†’ ğ“¥ n) â†’ ğ“¢ (max m n) (|Î£| m n a b) â‰¡â‚ (Î£ x ê‰ ğ“¢ m a , ğ“¢ n (b x))
  |Î |-eq  : (m n : â„•) â†’ (a : ğ“¥ m) (b : ğ“¢ m a â†’ ğ“¥ n) â†’ ğ“¢ (max m n) (|Î | m n a b) â‰¡â‚ (Î  x ê‰ ğ“¢ m a , ğ“¢ n (b x))
  |W|-eq  : (m n : â„•) â†’ (a : ğ“¥ m) (b : ğ“¢ m a â†’ ğ“¥ n) â†’ ğ“¢ (max m n) (|W| m n a b) â‰¡â‚ (W x ê‰ ğ“¢ m a , ğ“¢ n (b x))
  |U|-eq : (n : â„•) â†’ ğ“¢ (succ n) (|U| (succ n)) â‰¡â‚ ğ“¥ n
  |T|-eq : (n : â„•) (a : ğ“¥ n) â†’ ğ“¢ (succ n) (|T| n a) â‰¡â‚ ğ“¢ n a

\end{code}

Some of them hold definitionally, but the others don't. Moreover, as
alluded above, they seem to require extensionality, which would be
removable if we replaced type equality â‰¡â‚ by type equivalence â‰ƒ in the
above equations.

\begin{code}

  |â„•â‚€|-eq n       = refl _
  |â„•â‚|-eq n       = refl _
  |â„•|-eq  n       = refl _
  |+|-eq  m n a b = t
   where
    p : ğ“¢ (max m n) (|+| m n a b) â‰¡â‚ ğ“¢ (max m n) (lift-L-max m n a) + ğ“¢ (max m n) (lift-R-max m n b)
    p = refl _
    r : ğ“¢ (max m n) (lift-L-max m n a) â‰¡â‚ ğ“¢ m a
    r = ğ“¢-L-max-eq m n a
    s : ğ“¢ (max m n) (lift-R-max m n b) â‰¡â‚ ğ“¢ n b
    s = ğ“¢-R-max-eq m n b
    t : ğ“¢ (max m n) (|+| m n a b) â‰¡â‚ ğ“¢ m a + ğ“¢ n b
    t = transportâ‚ (Î» - â†’ ğ“¢ (max m n) (|+| m n a b) â‰¡â‚ ğ“¢ m a + -) s
         (transportâ‚ (Î» - â†’ ğ“¢ (max m n) (|+| m n a b) â‰¡â‚ - + ğ“¢ (max m n) (lift-R-max m n b)) r p)
  |Î£|-eq  m n a b = t
   where
    A : Set
    A = ğ“¢ (max m n) (lift-L-max m n a)
    B : A â†’ Set
    B x = ğ“¢ (max m n) (lift-R-max m n (b (ğ“¢-L-max m n a x)))
    p : ğ“¢ (max m n) (|Î£| m n a b) â‰¡â‚ Î£ x ê‰ A , B x
    p = refl _
    r : A â‰¡â‚ ğ“¢ m a
    r = ğ“¢-L-max-eq m n a
    s : (x : A) â†’ B x â‰¡â‚ ğ“¢ n (b (ğ“¢-L-max m n a x))
    s x = ğ“¢-R-max-eq m n (b (ğ“¢-L-max m n a x))
    t : ğ“¢ (max m n) (|Î£| m n a b) â‰¡â‚ Î£ y ê‰ ğ“¢ m a , ğ“¢ n (b y)
    t = change-of-variable Î£ A (ğ“¢ m a) B (Î» x â†’ ğ“¢ n (b x)) r s Î£-ext
  |Î |-eq  m n a b = t
   where
    A : Set
    A = ğ“¢ (max m n) (lift-L-max m n a)
    B : A â†’ Set
    B x = ğ“¢ (max m n) (lift-R-max m n (b (ğ“¢-L-max m n a x)))
    p : ğ“¢ (max m n) (|Î | m n a b) â‰¡â‚ Î  x ê‰ A , B x
    p = refl _
    r : A â‰¡â‚ ğ“¢ m a
    r = ğ“¢-L-max-eq m n a
    s : (x : A) â†’ B x â‰¡â‚ ğ“¢ n (b (ğ“¢-L-max m n a x))
    s x = ğ“¢-R-max-eq m n (b (ğ“¢-L-max m n a x))
    t : ğ“¢ (max m n) (|Î | m n a b) â‰¡â‚ Î  x ê‰ ğ“¢ m a , ğ“¢ n (b x)
    t = change-of-variable Î  A (ğ“¢ m a) B (Î» x â†’ ğ“¢ n (b x)) r s Î -ext
  |W|-eq  m n a b = t
   where
    A : Set
    A = ğ“¢ (max m n) (lift-L-max m n a)
    B : A â†’ Set
    B x = ğ“¢ (max m n) (lift-R-max m n (b (ğ“¢-L-max m n a x)))
    p : ğ“¢ (max m n) (|W| m n a b) â‰¡â‚ W x ê‰ A , B x
    p = refl _
    r : A â‰¡â‚ ğ“¢ m a
    r = ğ“¢-L-max-eq m n a
    s : (x : A) â†’ B x â‰¡â‚ ğ“¢ n (b (ğ“¢-L-max m n a x))
    s x = ğ“¢-R-max-eq m n (b (ğ“¢-L-max m n a x))
    t : ğ“¢ (max m n) (|W| m n a b) â‰¡â‚ W x ê‰ ğ“¢ m a , ğ“¢ n (b x)
    t = change-of-variable W A (ğ“¢ m a) B (Î» x â†’ ğ“¢ n (b x)) r s W-ext

  |U|-eq  n       = refl _
  |T|-eq  n a     = refl _

\end{code}

(This is the end of the anonymous module.)

However, without extensionality, it should be a meta-theorem that the
desired equations hold definitionally for any numeral. We test this
conjecture with 7 and 13:

\begin{code}

sample-|+|-eq  : (a : ğ“¥ 7)  (b : ğ“¥ 13)         â†’ ğ“¢ 13 (|+| 7 13 a b) â‰¡â‚ (ğ“¢ 7 a + ğ“¢ 13 b)
sample-|Î£|-eq  : (a : ğ“¥ 7)  (b : ğ“¢ 7 a â†’ ğ“¥ 13) â†’ ğ“¢ 13 (|Î£| 7 13 a b) â‰¡â‚ (Î£ x ê‰ ğ“¢ 7 a , ğ“¢ 13 (b x))
sample-|Î |-eq  : (a : ğ“¥ 7)  (b : ğ“¢ 7 a â†’ ğ“¥ 13) â†’ ğ“¢ 13 (|Î | 7 13 a b) â‰¡â‚ (Î  x ê‰ ğ“¢ 7 a , ğ“¢ 13 (b x))
sample-|W|-eq  : (a : ğ“¥ 13) (b : ğ“¢ 13 a â†’ ğ“¥ 7) â†’ ğ“¢ 13 (|W| 13 7 a b) â‰¡â‚ (W x ê‰ ğ“¢ 13 a , ğ“¢ 7 (b x))

sample-|+|-eq  a b = refl _
sample-|Î£|-eq  a b = refl _
sample-|Î |-eq  a b = refl _
sample-|W|-eq  a b = refl _

\end{code}
